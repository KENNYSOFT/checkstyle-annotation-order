plugins {
    id 'java'
    id 'checkstyle'
    id 'maven-publish'
}

group 'kr.kennysoft'
version '0.1.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.puppycrawl.tools:checkstyle:8.41'
}

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion = '8.41'
    configFile = 'checkstyle.xml' as File
    ignoreFailures = false
    maxWarnings = 0
}

tasks.withType(Checkstyle) {
    checkstyleClasspath += checkstyleMain.classpath
}

tasks.withType(Jar) {
    dependsOn checkstyleMain, checkstyleTest
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/KENNYSOFT/checkstyle-annotation-order"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
